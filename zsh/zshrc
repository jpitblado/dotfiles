# zsh/zshrc

export DOTFILES=$HOME/.dotfiles

# build unique lists of resource files
typeset -U shrcFiles
shrcFiles=($DOTFILES/**/*.shrc)
typeset -U zrcFiles
zrcFiles=($DOTFILES/**/*.zshrc)

. $DOTFILES/sh/debug_msg.sh

# load all shrc files
for file in ${shrcFiles}
do
	debug_msg "sourcing $file"
	source $file
done

# load all path files
for file in ${(M)zrcFiles:#*/path.zshrc}
do
	debug_msg "sourcing $file"
	source $file
done

# load everything but path and completion files
for file in ${${zrcFiles:#*/path.zshrc}:#*/completion.zshrc}
do
	debug_msg "sourcing $file"
	source $file
done

# initialize autocomplete
autoload -U compinit
compinit -u

# load all completion files
for file in ${(M)zrcFiles:#*/completion.zshrc}
do
	debug_msg "sourcing $file"
	source $file
done

unset pathmunge
unset debug_msg
unset zrcFiles
unset shrcFiles

unsetopt share_history

# end: zsh/zshrc
